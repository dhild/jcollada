/*
 * Copyright (c) 2014 D. Ryan Hild <d.ryan.hild@gmail.com>
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

plugins {
    id 'groovy'
}

configurations {
    hk2Generator {
        extendsFrom compile
    }
    jaxb {
        extendsFrom compile
    }
}

sourceSets {
    generated {
        java {
            srcDirs = ["$projectDir/src/main/generated"]
        }
    }
    main {
        compileClasspath += sourceSets.generated.output
    }
}

dependencies {
    compile project(':jcollada-spi')
    compile project(':jcollada-common')

    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.11'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.11'
    jaxb 'com.sun.xml.bind:jaxb-core:2.2.11'
    jaxb 'com.sun.xml.bind:jaxb-ri:2.2.11'
    jaxb 'javax.xml.bind:jaxb-api:2.2.11'

    generatedCompile 'javax.xml.bind:jaxb-api:2.2.11'
    generatedCompile 'com.sun.xml.bind:jaxb-core:2.2.11'

    compile 'javax.xml.bind:jaxb-api:2.2.11'
    compile 'com.sun.xml.bind:jaxb-core:2.2.11'
    compile 'org.codehaus.woodstox:woodstox-core-asl:4.4.1'

    compile 'com.carrotsearch:hppc:0.6.0'
    provided 'org.projectlombok:lombok:1.16.2'

    compile "org.glassfish.hk2:hk2:$hk2Version"
    hk2Generator "org.glassfish.hk2:hk2-inhabitant-generator:$hk2Version"

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

task('xjc') {
    inputs.dir "$projectDir/src/main/resources/schema"
    outputs.dir "$projectDir/src/main/generated"
    doLast {
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.jaxb.asPath)

        file("$projectDir/src/main/generated").mkdirs()
        ant.xjc(schema: "$projectDir/src/main/resources/schema/collada_schema_1_5.xsd",
                package: 'org.collada.schema15',
                destdir: "$projectDir/src/main/generated",
                removeOldOutput: true,
                binding: "$projectDir/src/main/resources/schema/bindings.xjb",
                extension: true) {
            arg(value: "-Xlocator")
            arg(value: "-mark-generated")
        }
    }
}

compileJava.dependsOn 'xjc'

task('generateHK2', type: JavaExec) {
    outputs.dir "$buildDir/generated/hk2"
    inputs.dir sourceSets.main.output.classesDir
    dependsOn 'compileJava', 'processResources'

    classpath configurations.hk2Generator

    args "--locator", "JCollada-Schema-1.5",
            "--directory", "$buildDir/generated/hk2/META-INF/hk2-locator",
            "--file", sourceSets.main.output.classesDir.path

    main "org.jvnet.hk2.generator.HabitatGenerator"
}

task('copyHK2Test', type: Copy) {
    dependsOn 'processTestResources', 'generateHK2'
    from "$buildDir/generated/hk2"
    into sourceSets.test.output.resourcesDir
}

test.dependsOn 'copyHK2Test'

jar {
    dependsOn 'generateHK2'
    from {
        files("$buildDir/generated/hk2")
    }
    manifest {
        attributes("Implementation-Title": "JCollada for Collada Schema 1.5",
                "Implementation-Version": "$version",
                "Implementation-Vendor": "D. Ryan Hild",
                "Specification-Title": "JCollada SPI",
                "Specification-Version": "$version",
                "Specification-Vendor": "D. Ryan Hild")
    }
}
